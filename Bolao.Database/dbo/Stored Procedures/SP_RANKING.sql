
CREATE PROCEDURE [dbo].[SP_RANKING] (
	@codBolao INT = NULL
) AS
BEGIN
	DECLARE @QTD_CRITERIO INT
	DECLARE @QTD_REGISTRO INT
	DECLARE @COD_BOLAO INT
	DECLARE @COD_USUARIO INT
	DECLARE @COD_JOGO INT
	DECLARE @COD_CRITERIO INT
	DECLARE @VLR_PONTUACAO INT
	DECLARE @NUM_RANKING INT
	DECLARE @DSC_REGRA VARCHAR(1500)
	DECLARE @DSC_ORDEM VARCHAR(1500)
	DECLARE @SQL NVARCHAR(4000)
	DECLARE @CODIGO NVARCHAR(3)

	BEGIN TRAN

	SET @DSC_ORDEM = N''

	CREATE TABLE #RANKING (COD_USUARIO INT PRIMARY KEY, VLR_PONTUACAO INT NULL)

	DECLARE CSR_CRITERIO CURSOR FOR
		SELECT C.COD_CRITERIO, C.DSC_REGRA, C.VLR_PONTUACAO
		FROM CRITERIO C
		WHERE C.COD_CRITERIO > 0
		ORDER BY C.VLR_PONTUACAO DESC

	OPEN CSR_CRITERIO

	FETCH NEXT FROM CSR_CRITERIO INTO @COD_CRITERIO, @DSC_REGRA, @VLR_PONTUACAO

	WHILE @@FETCH_STATUS = 0 BEGIN
		SET @CODIGO = CONVERT(NVARCHAR(3), @COD_CRITERIO)
		SET @DSC_ORDEM = @DSC_ORDEM + N'QTD_CRITERIO_' + @CODIGO + N' DESC, '
		SET @SQL = N'ALTER TABLE #RANKING ADD QTD_CRITERIO_' + @CODIGO + N' INT NULL'

		EXEC SP_EXECUTESQL @SQL

		FETCH NEXT FROM CSR_CRITERIO INTO @COD_CRITERIO, @DSC_REGRA, @VLR_PONTUACAO
	END

	IF @DSC_ORDEM <> N'' BEGIN
		SET @DSC_ORDEM = LEFT(@DSC_ORDEM, LEN(@DSC_ORDEM) - 1)
	END 

	CLOSE CSR_CRITERIO

	DECLARE CSR_BOLAO CURSOR FOR
		SELECT B.COD_BOLAO
		FROM BOLAO B
		WHERE B.IND_STATUS = 'A'
			AND (@codBolao IS NULL OR B.COD_BOLAO = @codBolao)
		ORDER BY B.COD_BOLAO ASC

	OPEN CSR_BOLAO

	FETCH NEXT FROM CSR_BOLAO INTO @COD_BOLAO

	WHILE @@FETCH_STATUS = 0 BEGIN

		DECLARE CSR_PARTICIPANTE CURSOR FOR
			SELECT P.COD_USUARIO
			FROM PARTICIPANTE P 
				INNER JOIN USUARIO U ON P.COD_USUARIO = U.COD_USUARIO
			WHERE P.IND_BOLAO_PAGO = 'S' AND P.COD_BOLAO = @COD_BOLAO
			ORDER BY U.NOM_USUARIO ASC

		OPEN CSR_PARTICIPANTE

		FETCH NEXT FROM CSR_PARTICIPANTE INTO @COD_USUARIO

		WHILE @@FETCH_STATUS = 0 BEGIN

			INSERT INTO #RANKING (COD_USUARIO, VLR_PONTUACAO) VALUES (@COD_USUARIO, 0)

			UPDATE APOSTA 
			SET COD_CRITERIO = NULL, IND_APURADA = 'N'
			WHERE COD_USUARIO = @COD_USUARIO AND
				COD_JOGO IN (SELECT COD_JOGO FROM JOGO WHERE COD_BOLAO = @COD_BOLAO)

			OPEN CSR_CRITERIO

			FETCH NEXT FROM CSR_CRITERIO INTO @COD_CRITERIO, @DSC_REGRA, @VLR_PONTUACAO

			WHILE @@FETCH_STATUS = 0 BEGIN

				SET @SQL = CONVERT(NVARCHAR(4000), @DSC_REGRA)

				SET @QTD_CRITERIO = 0

				DECLARE @CSR_JOGO CURSOR

				EXEC SP_EXECUTESQL @SQL, 
					N'@CSR_JOGO CURSOR OUTPUT, @COD_BOLAO INT, @COD_USUARIO INT', 
					@CSR_JOGO OUTPUT, @COD_BOLAO, @COD_USUARIO

				FETCH NEXT FROM @CSR_JOGO INTO @COD_JOGO

				WHILE @@FETCH_STATUS = 0 BEGIN

					UPDATE APOSTA SET 
						IND_APURADA = 'S',
						COD_CRITERIO = @COD_CRITERIO
					WHERE COD_JOGO = @COD_JOGO AND
						COD_USUARIO = @COD_USUARIO

					SET @QTD_CRITERIO = @QTD_CRITERIO + 1

					FETCH NEXT FROM @CSR_JOGO INTO @COD_JOGO

				END

				SET @QTD_REGISTRO = @QTD_CRITERIO
				SET @CODIGO = CONVERT(NVARCHAR(3), @COD_CRITERIO)
				SET @SQL = N'UPDATE #RANKING SET VLR_PONTUACAO = VLR_PONTUACAO + (@QTD_REGISTRO * @VLR_PONTUACAO), QTD_CRITERIO_' + @CODIGO + ' = @QTD_REGISTRO WHERE COD_USUARIO = @COD_USUARIO'

				EXEC SP_EXECUTESQL @SQL,
					N'@QTD_REGISTRO INT, @VLR_PONTUACAO INT, @COD_USUARIO INT',
					@QTD_REGISTRO, @VLR_PONTUACAO, @COD_USUARIO

				CLOSE @CSR_JOGO
				DEALLOCATE @CSR_JOGO

				FETCH NEXT FROM CSR_CRITERIO INTO @COD_CRITERIO, @DSC_REGRA, @VLR_PONTUACAO
			END

			CLOSE CSR_CRITERIO

			UPDATE APOSTA
			SET IND_APURADA = 'S', COD_CRITERIO = 0
			WHERE IND_APURADA = 'N' AND
				COD_USUARIO = @COD_USUARIO AND
				COD_JOGO IN (SELECT COD_JOGO FROM JOGO WHERE COD_BOLAO = @COD_BOLAO AND IND_REALIZADO = 'S')

			FETCH NEXT FROM CSR_PARTICIPANTE INTO @COD_USUARIO

		END

		CLOSE CSR_PARTICIPANTE
		DEALLOCATE CSR_PARTICIPANTE

		SET @NUM_RANKING = 1
		SET @SQL = N'SET @CSR_RANKING = CURSOR FOR SELECT COD_USUARIO FROM #RANKING ORDER BY VLR_PONTUACAO DESC, ' + @DSC_ORDEM + N'; OPEN @CSR_RANKING;'

		DECLARE @CSR_RANKING CURSOR

		EXEC SP_EXECUTESQL @SQL,
			N'@CSR_RANKING CURSOR OUTPUT', 
			@CSR_RANKING OUTPUT

		FETCH NEXT FROM @CSR_RANKING INTO @COD_USUARIO

		WHILE @@FETCH_STATUS = 0 BEGIN

			SELECT @VLR_PONTUACAO = COALESCE(SUM(B.VLR_PONTUACAO), 0)
			FROM APOSTA A
				INNER JOIN CRITERIO B ON A.COD_CRITERIO = B.COD_CRITERIO
				INNER JOIN JOGO C ON A.COD_JOGO = C.COD_JOGO
			WHERE A.COD_USUARIO = @COD_USUARIO AND C.COD_BOLAO = @COD_BOLAO

			UPDATE PARTICIPANTE 
			SET NUM_RANKING = @NUM_RANKING, VLR_PONTUACAO = @VLR_PONTUACAO
			WHERE COD_USUARIO = @COD_USUARIO AND
				COD_BOLAO = @COD_BOLAO

			SET @NUM_RANKING = @NUM_RANKING + 1

			FETCH NEXT FROM @CSR_RANKING INTO @COD_USUARIO

		END

		CLOSE @CSR_RANKING

		DEALLOCATE @CSR_RANKING

		DELETE FROM #RANKING

		UPDATE BOLAO SET DAT_RANKING = getdate() WHERE COD_BOLAO = @COD_BOLAO

		FETCH NEXT FROM CSR_BOLAO INTO @COD_BOLAO

	END

	CLOSE CSR_BOLAO

	DEALLOCATE CSR_BOLAO

	DEALLOCATE CSR_CRITERIO

	COMMIT TRAN
	
END